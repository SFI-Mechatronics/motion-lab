<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.12">
  <POU Name="EulerToBody" Id="{c257be1f-55b7-49fc-9e40-9565df3fd57c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK EulerToBody
VAR_INPUT
	roll : REAL := 0.0;
	pitch : REAL := 0.0;
	yaw : REAL := 0.0;
	
	roll_t : REAL := 0.0;
	pitch_t : REAL := 0.0;
	yaw_t : REAL := 0.0;
	
	roll_tt : REAL := 0.0;
	pitch_tt : REAL := 0.0;
	yaw_tt : REAL := 0.0;
END_VAR
VAR_OUTPUT
	wx : REAL := 0.0;
	wy : REAL := 0.0;
	wz : REAL := 0.0;
	
	wx_t : REAL := 0.0;
	wy_t : REAL := 0.0;
	wz_t : REAL := 0.0;
END_VAR
VAR
	phi : ARRAY [0..2] OF REAL;
	phi_t : ARRAY [0..2] OF REAL;
	phi_tt : ARRAY [0..2] OF REAL;
	
	w : ARRAY [0..2] OF REAL;
	w_t : ARRAY [0..2] OF REAL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Positions
phi[0] := roll;
phi[1] := pitch;
phi[2] := yaw;

// Velocities
phi_t[0] := roll_t;
phi_t[1] := pitch_t;
phi_t[2] := yaw_t;

// Accelerations
phi_tt[0] := roll_tt;
phi_tt[1] := pitch_tt;
phi_tt[2] := yaw_tt;

// Transform from phi_t to w
w[0] := phi_t[1]*SIN(phi[2]) + phi_t[0]*COS(phi[1])*COS(phi[2]);
w[1] := phi_t[1]*COS(phi[2]) - phi_t[0]*COS(phi[1])*SIN(phi[2]);
w[2] := phi_t[2] + phi_t[0]*SIN(phi[1]);

// Transform from phi_tt to w_t
w_t[0] := SIN(phi[2])*phi_tt[1] + COS(phi[2])*phi_t[1]*phi_t[2] + COS(phi[1])*COS(phi[2])*phi_tt[0] - COS(phi[2])*SIN(phi[1])*phi_t[0]*phi_t[1] - COS(phi[1])*SIN(phi[2])*phi_t[0]*phi_t[2];
w_t[1] := COS(phi[2])*phi_tt[1] - SIN(phi[2])*phi_t[1]*phi_t[2] - COS(phi[1])*SIN(phi[2])*phi_tt[0] - COS(phi[1])*COS(phi[2])*phi_t[0]*phi_t[2] + SIN(phi[1])*SIN(phi[2])*phi_t[0]*phi_t[1];
w_t[2] := SIN(phi[1])*phi_tt[0] + phi_tt[2] + COS(phi[1])*phi_t[0]*phi_t[1];

// Rotational velocites in body coordinates
wx := w[0];
wy := w[1];
wz := w[2];

// Rotational accelerations in body coordinates
wx_t := w_t[0];
wy_t := w_t[1];
wz_t := w_t[2];


]]></ST>
    </Implementation>
    <LineIds Name="EulerToBody">
      <LineId Id="52" Count="0" />
      <LineId Id="50" Count="1" />
      <LineId Id="48" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="55" Count="3" />
      <LineId Id="54" Count="0" />
      <LineId Id="59" Count="2" />
      <LineId Id="49" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="15" Count="1" />
      <LineId Id="18" Count="4" />
      <LineId Id="62" Count="2" />
      <LineId Id="66" Count="1" />
      <LineId Id="70" Count="4" />
      <LineId Id="68" Count="1" />
      <LineId Id="65" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>